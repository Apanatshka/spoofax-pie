module start
imports lex/lex

context-free start-symbols Program Exp

sorts Program Stat
context-free syntax

  Program.Program = <<{Stat "\n"}+>>

  Stat.Exp  = <<Exp>;>
  Stat.Bind = <<ID> = <Exp>;>

sorts Exp UnOp BinOp
context-free syntax

  Exp = UnOp
  Exp = BinOp

context-free syntax // numbers

  Exp = <(<Exp>)> {bracket}

  Exp.Num = NUM

  UnOp.Min = <-<Exp>>

  BinOp.Pow = <<Exp> ^ <Exp>> {right}
  BinOp.Mul = <<Exp> * <Exp>> {left}
  BinOp.Div = <<Exp> / <Exp>> {left}
  BinOp.Sub = <<Exp> - <Exp>> {left, prefer}
  BinOp.Add = <<Exp> + <Exp>> {left}
  BinOp.Eq  = <<Exp> == <Exp>> {non-assoc}
  BinOp.Neq = <<Exp> != <Exp>> {non-assoc}
  BinOp.Gt  = [[Exp] > [Exp]]  {non-assoc}
  BinOp.Lt  = [[Exp] < [Exp]]  {non-assoc}

context-free syntax // booleans

  Exp.True  = "true"
  Exp.False = "false"
  UnOp.Not   = <!<Exp>>
  BinOp.And   = <<Exp> & <Exp>> {left}
  BinOp.Or    = <<Exp> | <Exp>> {left}

  Exp.If = <
    if(<Exp>)
      <Exp>
    else
      <Exp>
  >

context-free syntax // variables and functions

  Exp.Var = ID
  Exp.Let = <
    let <ID> = <Exp> in
    <Exp>
  >
  Exp.Fun = <\\ <ID+> . <Exp>>
  Exp.App = <<Exp> <Exp>> {left}

context-free priorities
  UnOp.Min
  > Exp.App
  > BinOp.Pow
  > {left: BinOp.Mul BinOp.Div}
  > {left: BinOp.Add BinOp.Sub}
  > {non-assoc: BinOp.Eq BinOp.Neq BinOp.Gt BinOp.Lt}
  > UnOp.Not
  > BinOp.And
  > BinOp.Or
  > Exp.If
  > Exp.Let
  > Exp.Fun

sorts Type
context-free syntax
  Type.NumT  = <Num>
  Type.BoolT = <Bool>
  Type.FunT  = [[Exp] -> [Exp]] {right}
  Type       = <(<Type>)> {bracket}

template options
  ID = keyword {reject}
