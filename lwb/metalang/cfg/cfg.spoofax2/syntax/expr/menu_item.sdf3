module expr/menu_item

imports

  expr
  command

context-free sorts

  MenuItem
  CommandActionProperty
  
context-free syntax

  MenuItem.Separator = <separator>
  MenuItem.Menu = <menu <Expr> [
  <{Expr "\n"}*>
]>
  MenuItem.CommandAction = <command-action {
    <{CommandActionProperty "\n"}*>
  }>
  Expr.MenuItem = <<MenuItem>>

  CommandActionProperty.CommandActionDisplayName   = <display-name = <Expr>>
  CommandActionProperty.CommandActionDescription   = <description = <Expr>>
  CommandActionProperty.CommandActionDef           = <command-def = <Expr>>
  CommandActionProperty.CommandActionExecutionType = <execution-type = <ExecutionType>>
  // TODO: initial arguments
  CommandActionProperty.CommandActionRequiredEditorSelectionTypes      = <required-editor-selection-types = [<{EditorSelectionType ", "}+>]>
  CommandActionProperty.CommandActionRequiredEditorFileTypes           = <required-editor-file-types = [<{EditorFileType ","}+>]>
  CommandActionProperty.CommandActionRequiredHierarchicalResourceTypes = <required-resource-types = [<{HierarchicalResourceType ", "}+>]>
  CommandActionProperty.CommandActionRequiredEnclosingResourceTypes    = <required-enclosing-command-contexts = [<{EnclosingCommandContext ", "}+>]>

context-free sorts // Editor selection type

  EditorSelectionType
  
context-free syntax

  EditorSelectionType.Region = <Region>
  EditorSelectionType.Offset = <Offset>
  
context-free sorts // Editor file type

  EditorFileType
  
context-free syntax

  EditorFileType.HierarchicalResource = <HierarchicalResource>
  EditorFileType.Resource             = <Resource>

context-free sorts // Hierarchical resource type

  HierarchicalResourceType
  
context-free syntax

  //HierarchicalResourceType.Project   = <Project>
  HierarchicalResourceType.Directory = <Directory>
  HierarchicalResourceType.File      = <File>

lexical syntax // Keywords

  Keyword = "separator"
  Keyword = "menu"
